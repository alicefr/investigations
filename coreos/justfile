os := "fcos"

# Tag: quay.io/trusted-execution-clusters/fedora-coreos:42.20251012.2.0
fcos_base_img:="quay.io/trusted-execution-clusters/fedora-coreos@sha256:6997f51fd27d1be1b5fc2e6cc3ebf16c17eb94d819b5d44ea8d6cf5f826ee773"
scos_base_img:= "quay.io/okd/scos-content@sha256:3813e6608a999756931d3d621932af9662860e71a552b2670f9fe320bf0d3585"
rhcos_base_img:= "quay.io/openshift-release-dev/ocp-v4.0-art-dev@sha256:905508f17491d092ce5c81283cac740bb7616d2dcf1947c883849a3a76cf9c4f"

fcos_img:= "quay.io/trusted-execution-clusters/fcos"
scos_img:= "quay.io/trusted-execution-clusters/scos"
rhcos_img:= "quay.io/trusted-execution-clusters/rhcos"

fcos_name:= "fedora-coreos"
scos_name:= "centos-stream-coreos"
rhcos_name:= "redhat-coreos"

fcos_config:= "https://github.com/coreos/fedora-coreos-config"
scos_config:= "https://github.com/coreos/rhel-coreos-config.git"
rhcos_config:= scos_config

base := if os == "rhcos" { rhcos_base_img } else { if os == "scos" { scos_base_img } else { fcos_base_img }}
image:= if os == "rhcos" { rhcos_img } else { if os == "scos" { scos_img } else { fcos_img} }
os_name := if os == "rhcos" { rhcos_name } else { if os == "scos" { scos_name } else { fcos_name } }
config := if os == "rhcos" { rhcos_config } else { if os == "scos" { scos_config } else { fcos_config } }

archive := os + ".ociarchive"

full_name := os_name
label := os_name

kbc_image := "quay.io/trusted-execution-clusters/trustee-attester:fedora-b13fd8a"
clevis_pin_trustee_image := "quay.io/trusted-execution-clusters/clevis-pin-trustee"
ignition_image := "ghcr.io/trusted-execution-clusters/ignition:20260112-85608d6"

info:
    @echo "os {{os}}"
    @echo "base {{base}}"
    @echo "image {{image}}"
    @echo "os_name {{os_name}}"
    @echo "label {{label}}"
    @echo "kbc_image {{kbc_image}}"
    @echo "clevis_pin_trustee_image {{clevis_pin_trustee_image}}"
    @echo "config {{config}}"


build:
    sudo podman build --no-cache \
           --build-arg BASE={{base}} \
           --build-arg COM_COREOS_OSNAME={{label}} \
           --build-arg KBC_IMG={{kbc_image}} \
           --build-arg CLEVIS_PIN_IMG={{clevis_pin_trustee_image}} \
           --build-arg IGNITION_IMG={{ignition_image}} \
           -t {{image}} -f Containerfile .


oci-archive:
    sudo skopeo copy containers-storage:{{image}} oci-archive:{{archive}}

# Reusable cosa function definition
cosa_function := '''
    #!/usr/bin/env bash
    cosa() {
        env | grep COREOS_ASSEMBLER || true

        # Default container image
        COREOS_ASSEMBLER_CONTAINER_LATEST="quay.io/coreos-assembler/coreos-assembler:latest"
        sudo podman pull $COREOS_ASSEMBLER_CONTAINER_LATEST

        set -ex
        sudo podman run --rm -ti --security-opt=label=disable --privileged -u 0 \
				--network host \
            -v=${PWD}:/srv/ --device=/dev/kvm --device=/dev/fuse \
            --tmpfs=/tmp -v=/var/tmp:/var/tmp --name=cosa \
            ${COREOS_ASSEMBLER_CONFIG_GIT:+-v=$COREOS_ASSEMBLER_CONFIG_GIT:/srv/src/config/:ro} \
            ${COREOS_ASSEMBLER_GIT:+-v=$COREOS_ASSEMBLER_GIT/src/:/usr/lib/coreos-assembler/:ro} \
            ${COREOS_ASSEMBLER_ADD_CERTS:+-v=/etc/pki/ca-trust:/etc/pki/ca-trust:ro} \
            ${COREOS_ASSEMBLER_CONTAINER_RUNTIME_ARGS} \
            ${COREOS_ASSEMBLER_CONTAINER:-$COREOS_ASSEMBLER_CONTAINER_LATEST} "$@"
    }
'''

init:
    #!/usr/bin/env bash
    {{cosa_function}}
    mkdir -p cache
    cp {{archive}} cache/{{archive}}
    cd cache
    cosa init --force {{config}}
    cosa import oci-archive:/srv/{{archive}}

build-qemu:
    #!/usr/bin/env bash
    {{cosa_function}}
    cd cache
    cosa osbuild qemu

kubevirt:
    #!/usr/bin/env bash
    {{cosa_function}}
    cd cache
    cosa osbuild kubevirt

azure:
    #!/usr/bin/env bash
    {{cosa_function}}
    cd cache
    cosa osbuild azure
